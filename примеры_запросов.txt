Рекомендации по интересам человека
PREFIX : <http://www.semanticweb.org/lesunvo/ontologies/2025/10/course-recommendation-ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?человек ?курс ?название_курса ?область_интереса
WHERE {
  ?человек :имя "Александр" ;
          :имеет_интересы ?область_интереса .
  
  ?курс :название_курса ?название_курса ;
        :относится_к_области ?область_интереса .
}
ORDER BY ?человек ?курс


Рекомендации для начинающих (низкая сложность + бесплатные)
PREFIX : <http://www.semanticweb.org/lesunvo/ontologies/2025/10/course-recommendation-ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?человек ?имя ?курс ?название_курса ?уровень_сложности ?стоимость
WHERE {
  ?человек :имя ?имя ;
          :имеет_интересы ?область_интереса .
  
  ?курс :название_курса ?название_курса ;
        :уровень_сложности ?уровень_сложности ;
        :стоимость ?стоимость ;
        :относится_к_области ?область_интереса .
  
  FILTER (?уровень_сложности = "Начальный" && ?стоимость = 0)
}
ORDER BY ?человек ?курс


Рекомендации курсов, которые еще не пройдены
PREFIX : <http://www.semanticweb.org/lesunvo/ontologies/2025/10/course-recommendation-ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?человек ?имя ?курс ?название_курса ?область_интереса
WHERE {
  ?человек :имя "Мария" ;
          :имя ?имя ;
          :имеет_интересы ?область_интереса .
  
  ?курс :название_курса ?название_курса ;
        :относится_к_области ?область_интереса .
 
  FILTER NOT EXISTS {
    ?запись :пройден_человеком ?человек ;
            :проходит_курс ?курс .
  }
}
ORDER BY ?человек ?курс



Рекомендация со всем
PREFIX : <http://www.semanticweb.org/lesunvo/ontologies/2025/10/course-recommendation-ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?человек ?имя ?курс ?название_курса 
       ?уровень_сложности ?продолжительность ?стоимость ?свободное_время
       (COALESCE(AVG(?оценка_значение), 0) as ?средний_рейтинг)
       (COUNT(DISTINCT ?отзыв) as ?количество_отзывов)
WHERE {
  ?человек :имя "Александр" ;
          :имя ?имя ;
          :свободное_время ?свободное_время ;
          :имеет_интересы ?область_интереса .
  
  ?курс :название_курса ?название_курса ;
        :уровень_сложности ?уровень_сложности ;
        :продолжительность ?продолжительность ;
        :стоимость ?стоимость ;
        :относится_к_области ?область_интереса .
  
  OPTIONAL { 
    ?курс :имеет_оценку ?оценка .
    ?оценка :значение_оценки ?оценка_значение . 
  }
  OPTIONAL { 
    ?курс :содержит_отзыв ?отзыв . 
  }
}
GROUP BY ?человек ?имя ?курс ?название_курса ?уровень_сложности ?продолжительность ?стоимость ?свободное_время
ORDER BY DESC(?средний_рейтинг) DESC(?количество_отзывов) ASC(?стоимость)